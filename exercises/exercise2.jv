pipeline TreePlanting {

    // Describe the structure of the pipeline, by connecting blocks via pipes. 
    TreeDataExtractor
        -> BaumartDeutschDeleter
        -> TreeTableInterpreter
        -> TreeDataLoader;

    // Extracte the data as a CSV file with ; seperator
    block TreeDataExtractor oftype CSVExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
        delimiter: ";";
    }

    // Interprete the sheet into table by defining a valuetype per column that specifies the data type of the column
    // Rows that include values that are not valid according to the their valuetypes are dropped automatically
    block TreeTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "lfd_nr" oftype integer,
            "stadtteil" oftype stadtteil,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "id" oftype id,
            "baumfamilie" oftype text,
        ];
    }
    
    // Delete column baumart_deutsch from data sheet
    // Column E is ralated to baumart_deutsch
    block BaumartDeutschDeleter oftype ColumnDeleter {
        delete: [column E];
    }

    // Define the valid value for stadtteil, it must start with Furth-
    valuetype stadtteil oftype text {
        constraints: [startWithFurth];
    }

    constraint startWithFurth oftype RegexConstraint {
        regex: /^Furth-.*/;
    }

    // Define the valid value for id, it should have this format:
    // {geo-coordinate 1}, {geo-coordinate 2}
    // a geo-coordinate is defined as {1-3 numbers}.{numbers}
    valuetype id oftype text {
        constraints: [idFormat];
    }

    constraint idFormat oftype RegexConstraint {
        regex: /\d{1,3}\.\d+, \d{1,3}\.\d+/;
    }

    // Use this block to load the cleaned Table into SQLite file
    block TreeDataLoader oftype SQLiteLoader {
        table: "trees";
        file: "./trees.sqlite";
    }

}
